#autoload -U

# Based on bashfinder: https://github.com/NapoleonWils0n/bashfinder.git


function --update-finder-with-cwd()
{
  case ${1} in
    list|icon|column)
      count=${2-1}
      view="set the current view of the front Finder window to ${1} view"
      ;;
    *)
      count=${1-1}
      view=
      ;;
  esac

  --run-with-no-dyld-vars osascript > /dev/null <<EOS
tell application "Finder"
  if (${count} <= (count Finder windows)) then
    set the target of window ${count} to (POSIX file "${PWD}") as string
  else
    open (POSIX file "${PWD}") as string
  end if
  ${view}
end tell
EOS
}

function --get-frontmost-finder-path()
{
  --run-with-no-dyld-vars osascript <<EOS
tell application "Finder"
  if (${1-1} <= (count Finder windows)) then
    get POSIX path of (target of window ${1-1} as alias)
  else
    get POSIX path of (desktop as alias)
  end if
end tell
EOS
}

case "${1}" in
  track)
    if (( ${+chpwd_functions[(r)--update-finder-with-cwd]} )); then
      print "Finder is already tracking the current directory."
    else
      chpwd_functions+=--update-finder-with-cwd
      --update-finder-with-cwd
    fi
    ;;
  untrack)
    if (( ${+chpwd_functions[(r)--update-finder-with-cwd]} )); then
      chpwd_functions[(r)--update-finder-with-cwd]=
    fi
    ;;
  list|icon|column)
    --update-finder-with-cwd ${1}
    ;;
  cd)
    cd "$(--get-frontmost-finder-path)"
    ;;
  clean)
    # Recursively delete `.DS_Store` files
    find . -type f -name '*.DS_Store' -ls -delete
    ;;
  show-hidden)
    # Show/hide hidden files in Finder
    defaults write com.apple.Finder AppleShowAllFiles -bool false && killall Finder
    ;;
  hide-hidden)
    # Show/hide hidden files in Finder
    defaults write com.apple.Finder AppleShowAllFiles -bool true && killall Finder
    ;;
  *)
    [ -n "${1}" ] && printf >&2 "error: Unknown command ${1}.\n\n"
    cat >&2 <<EOS
usage: ${0} [track|untrack|cd]
       ${0} [list|icon|column]
       ${0} [clean|show-hidden|hide-hidden]

    track         Makes the frontmost Finder window track with the current
                  directory.
    untrack       Removes Finder directory tracking.
    cd            Changes the current directory to the current Finder path.
    list          Changes the frontmost Finder window to the list view of the
                  current path.
    icon          Changes the frontmost Finder window to the icon view of the
                  current path.
    column        Changes the frontmost Finder window to the column view of the
                  current path.
    clean         Removes .DS_Store files from the current directory down.

    show-hidden   Shows hidden files in the Finder.
    hide-hidden   Hides hidden files in the Finder.
EOS
    [ -n "${1}" ] && return 1
    ;;
esac
