#! zsh

autoload colors zsh/terminfo
[ "${terminfo[colors]}" -ge 8 ] && colors

function()
{
  if --hzsh-is-caching; then
    local cache
    assign-output-to cache --hzsh-cache-path
    [ -w "${cache}" ] && HISTFILE="${cache}/History"
  fi
}

# These reserved name arrays allow chaining of preexec, precmd, and chpwd
# functions.
typeset -ga preexec_functions
typeset -ga precmd_functions
typeset -ga chpwd_functions

HISTSIZE=70000                  # size of history
SAVEHIST=30000                  # maximum of history events to be save

LOGCHECK=60                     # watch login/logout every 60 seconds
watch=(notme)                   # watch login/logout

LISTMAX=-3

if has less; then
  export PAGER=less
  export LESS=FRX

  # this makes man pages look nicer...
  export LESS_TERMCAP_mb=$'\E[01;31m'
  export LESS_TERMCAP_md=$'\E[01;31m'
  export LESS_TERMCAP_me=$'\E[0m'
  export LESS_TERMCAP_se=$'\E[0m'
  export LESS_TERMCAP_so=$'\E[01;44;33m'
  export LESS_TERMCAP_ue=$'\E[0m'
  export LESS_TERMCAP_us=$'\E[01;32m'

  READNULLCMD=less
fi

export LSCOLORS="Gxfxcxdxbxegedabagacad"
export REPORTTIME=2 # report time if execution exceeds 2 seconds
export MANWIDTH=80

cdpath+=(~/.links/ ~/dev ~/dev/client ~/dev/backend ~/oss ~/oss/github ~)

function()
{ # Yanked from http://vincent.bernat.im/en/blog/2011-zsh-zshrc.html
  # Modified because I don't use emacs.
  local -a editors

  zstyle -a :hzsh editors editors
  (( ${#editors} )) || editors=("vim" "vi" "editor")

  for editor (${editors}); do
    (( ${+commands[${editor[(w)1]}]} )) &&
      {
        export EDITOR=$editor
        break
      }
  done
}
