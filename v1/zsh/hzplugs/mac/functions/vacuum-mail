#autoload -U

# Speed up Mail.app by vacuuming the Envelope Index
# Code from:
# http://www.hawkwings.net/2007/03/03/scripts-to-automate-the-mailapp-envelope-speed-trick/
#
# Originally by "pmbuko" with modifications by Romulo
# Updated by Brett Terpstra 2012
# Updated by Mathias Törnblom 2015 to support V3 in El Capitan and still keep
# backwards compatibility
#
# Made into a zsh script by Austin Ziegler 2016

local was_running mailv size_before size_after os_minor

was_running=false
is-app-running Mail.app && was_running=true

os_minor=$(sw_vers -productVersion | cut -f2 -d.)
mailv=V2
(( 10 <= ${os_minor} )) && mailv=V3
(( 12 <= ${os_minor} )) && mailv=V4

mail_path=~/Library/Mail/${mailv}/MailData

function size_of_index()
{
  command ls -lnah ${mail_path} | awk '/Envelope Index$/ { print $5; }'
}

${was_running} && {
  print "Closing Mail…"
  tell application Mail to quit
}

print "Cleaning the Envelope Index…"
size_before=$(size_of_index)
sqlite3 "${mail_path}/Envelope Index" vacuum
size_after=$(size_of_index)

cat <<EOF
Mail Index
     Before: ${size_before}
     After:  ${size_after}
EOF

${was_running} && {
  print "Restarting Mail…"
  tell application Mail to activate
}
true
