#! zsh

function()
{
  # Trim new lines and copy to clipboard
  alias c="tr -d '\n' | pbcopy"

  # One of @janmoesen’s ProTip™s
  for method in GET HEAD POST PUT DELETE TRACE OPTIONS; do
    alias "$method"="lwp-request -m '$method'"
  done

  # Stuff I never really use but cannot delete either because of http://xkcd.com/530/
  alias stfu="osascript -e 'set volume output muted true'"
  alias pumpitup="osascript -e 'set volume 7'"
  if has growlnotify; then
    alias hax="growlnotify -a 'Activity Monitor' 'System error' -m 'WTF R U DOIN'"
  fi

  # Gzip-enabled `curl`
  alias gurl="curl --compressed"

  # Flush Directory Service cache
  alias flush="dscacheutil -flushcache && killall -HUP mDNSResponder"

  # Clean up LaunchServices to remove duplicates in the “Open With” menu
  alias lscleanup="/System/Library/Frameworks/CoreServices.framework/Frameworks/LaunchServices.framework/Support/lsregister -kill -r -domain local -domain system -domain user && killall Finder"

  # View HTTP traffic
  alias sniff="sudo ngrep -d 'en1' -t '^(GET|POST) ' 'tcp and port 80'"
  alias httpdump="sudo tcpdump -i en1 -n -s 0 -w - | grep -a -o -E \"Host\: .*|GET \/.*\""

  if is-mac-at-least mountain lion; then
    # Alias setuid/setgid commands to eliminated the use of DYLD_* environment variables.
    function --run-with-no-dyld-vars()
    {
      local vars
      vars=($(env | grep -i DYLD_ | cut -f1 -d=))

      (( ${#vars} )) && local ${^vars}=

      "${@}"
    }

    for cmd ($path/*(sN:t) $^path/*(SN:t)); do
      alias "${cmd}"="--run-with-no-dyld-vars ${cmd}"
    done
  fi
}
