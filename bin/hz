#! /usr/bin/env zsh

if (( ${UID} == 0 )); then
  builtin print "${0} cannot be run as root." >&2
  exit 1
fi

unset REPORTTIME

export HZ_VERSION=1.0
export HZ_BN0=${0:t}
export HZ_DN0=$(builtin cd ${0:h}; builtin pwd -P)
export HZ_RUN_RELATIVE=true
[[ ${HZ_DN0} == ${0:h} ]] && HZ_RUN_RELATIVE=false
export HZ_ROOT=${HZ_DN0:h}
export HZ_BIN=${HZ_ROOT}/bin
export HZ_LIB=${HZ_ROOT}/lib
export HZ_LIBEXEC=${HZ_ROOT}/lib/exec
export HZ_INTERNAL=${HZ_ROOT}/.hz-internal
export HZ_TARGET=${HOME}

export HZ_OPTION_DEBUG=false
export HZ_OPTION_FORCE=false

builtin source ${HZ_LIB}/hz/core.zsh
builtin source ${HZ_LIB}/hz/core/commands.zsh

path+=(${HZ_BIN} ${HZ_LIBEXEC})

local -a hz_modified_args &&
  --hz-parse-global-opts "${@}" &&
  set -- "${hz_modified_args[@]}" &&
  unset hz_modified_args

local hz_command=help

(( ${#} )) && { hz_command=${1}; shift }

cd ${HZ_ROOT}

--hz-setup
--hz-unknown-command ${hz_command}

if --hz-has-function ${hz_command}; then
  hz-${hz_command} "${@}"
elif --hz-has-command ${hz_command}; then
  exec hz-${hz_command} "${@}"
fi
